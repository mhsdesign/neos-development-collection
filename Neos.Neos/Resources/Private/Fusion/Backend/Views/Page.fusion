prototype(Neos.Neos:View.Page) < prototype(Neos.Fusion:Component) {
  content = ''
  title = ''
  styles = ${[]}
  beforeHead = ''
  insideHead = ''

  renderer = Neos.Fusion:Join {
    doctype = '<!DOCTYPE html>'
    htmlStart = '<html lang="en">'
    head = afx`
      {props.beforeHead}
      <head>
        <meta charset="utf-8" />
        <meta name="robots" content="noindex" />
        <title>{props.title}</title>
        <Neos.Fusion:Loop items={props.styles}>
          <link rel="stylesheet" @children="attributes.href">
          <Neos.Fusion:ResourceUri path={item}/>
          </link>
        </Neos.Fusion:Loop>
        <link href={StaticResource.uri('Neos.Neos', 'Public/Images/Icons/apple-touch-icon.png')} sizes="180x180" rel="apple-touch-icon" />
        <link href={StaticResource.uri('Neos.Neos', 'Public/Images/Icons/favicon-16x16.png')} sizes="16x16" rel="icon" type="image/png" />
        <link href={StaticResource.uri('Neos.Neos', 'Public/Images/Icons/favicon-32x32.png')} sizes="32x32" rel="icon" type="image/png" />
        <link href={StaticResource.uri('Neos.Neos', 'Public/Images/Icons/safari-pinned-tab.svg')} rel="mask-icon" color="#00adee" />
        {props.insideHead}
      </head>
    `
    body = ${props.content}
    htmlEnd = '</html>'
  }
}
